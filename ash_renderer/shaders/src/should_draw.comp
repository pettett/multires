#version 460

#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_ARB_shader_draw_parameters : require
#extension GL_KHR_shader_subgroup_arithmetic : enable

#extension GL_EXT_debug_printf : enable

#include "structs\camera.glsl"
#include "structs\cluster_data.glsl"
#include "structs\model.glsl"

//@group(0) @binding(0) var<storage, write> result_indicies: array<i32>;

// layout(binding = 0) writeonly buffer ResultIndicies {
//     uint result[];
// } result_indicies;

// layout(binding = 1) writeonly buffer DrawIndirectParams {
//     DrawIndexedIndirect result[];
// } draw_indirect_params;

layout(binding = 0) readonly buffer ModelUniformBufferObject {
	Model models[];
};

layout(binding = 2) readonly buffer Clusters {
	ClusterData clusters[];
};

layout(binding = 1) writeonly buffer ShouldDraw {
	uint should_draw[];
};

layout(binding = 3) readonly uniform CameraUniformBufferObject {
	CameraUniformObject ubo;
};

#include "structs\indirect_dispatch_data.glsl"
layout(binding = 4) buffer IndirectDispatchBuffer {
	IndirectDispatchData indirect_dispatch[];
};

layout(push_constant) uniform constants {
	uint instance_index;
};

#include "util\cluster.glsl"
#include "util\culling.glsl"

const uint LOCAL_SIZE_X = 16;

layout(local_size_x = LOCAL_SIZE_X, local_size_y = 1, local_size_z = 1) in;

void main() {
	uvec3 id = gl_GlobalInvocationID;
	uint cluster_index = id.x;

	if (cluster_index == 0) {
		indirect_dispatch[instance_index].group_size_x -= 1;
	}

	vec3 local_cam_pos;
	bool high_error;
	bool draw = cluster_can_draw(cluster_index, instance_index, local_cam_pos, high_error);

	mat4 MVP = ubo.culling_view_proj * models[instance_index].model;
	// Generate object space clipping planes

	vec4 planes[6] = planes_from_mat(MVP);

	uint max_needed_workgroups = calc_max_needed_workgroups(draw, cluster_index, high_error, LOCAL_SIZE_X);
	uint subgroup_max_needed_workgroups = subgroupMax(max_needed_workgroups);

	if (subgroupElect()) {
		atomicMax(indirect_dispatch[instance_index].group_size_x, subgroup_max_needed_workgroups);
	}

	bool in_clip = sphere_inside_planes(planes, clusters[cluster_index].tight_sphere);

	if (draw && in_clip) {
		uint pos = atomicAdd(should_draw[0], 1);

		// debugPrintfEXT("%u, ", pos);

		should_draw[pos] = cluster_index + 1;
	} else {
		// should_draw[cluster_index + 1] = 0;
	}
}